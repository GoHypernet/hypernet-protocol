// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721Burnable.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/utils/Counters.sol";


/**
 * @dev {ERC721} compatible Registry, including:
 *
 *  - ability for holders or governance to burn (destroy) their tokens
 *  - a minter role that allows for token minting (creation)
 *  - token lookup via human readable string
 *
 * This contract uses {AccessControl} to lock permissioned functions using the
 * different roles - head to its documentation for details.
 *
 * The account that deploys the contract will be granted the minter and pauser
 * roles, as well as the default admin role, which will let it grant both minter
 * and pauser roles to other accounts.
 */
contract NonFungibleRegistry is
    Context,
    AccessControlEnumerable,
    ERC721Enumerable,
    ERC721Burnable,
    ERC721URIStorage
{
    using Counters for Counters.Counter;

    // mapping from agent name to its tokenID
    mapping(string => uint256) public registryMap;

    // reverse mapping from tokenID to its agent name
    mapping(uint256 => string) public reverseRegistryMap;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");

    Counters.Counter private _tokenIdTracker;

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, and `MINTER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor() ERC721("HypernetRegistry", "HR") {

        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());

        _setupRole(MINTER_ROLE, _msgSender());
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     */
    function register(address to, string memory _agentName, string memory registrationData) public virtual {
        require(hasRole(MINTER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have minter role to mint");
        require(!_mappingExists(_agentName), "NonFungibleRegistry: agent is already registered.");

        // We cannot just use balanceOf to create the new tokenId because tokens
        // can be burned (destroyed), so we need a separate counter.
        uint256 tokenId = _tokenIdTracker.current() + 1;
        _mint(to, tokenId);
        _tokenIdTracker.increment();
        _setTokenURI(tokenId, registrationData);

        // extend the registry mapping for lookup via gateway URL
        registryMap[_agentName] = tokenId;
        reverseRegistryMap[tokenId] = _agentName;
    }

    /**
     * @dev Updates the signature stored in the token at tokenId
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE` or own the token.
     */
    function updateRegistration(uint256 tokenId, string memory registrationData) public virtual {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "NonFungibleRegistry: caller is not owner nor approved");
        _setTokenURI(tokenId, registrationData);
    }

    /**
     * @dev Burns `tokenId`. See {ERC721-_burn}.
     *
     * Requirements:
     *
     * - The caller must own `tokenId` or be an approved operator.
     */
    function _burn(
        uint256 tokenId
    ) 
        internal 
        virtual 
        override(ERC721, ERC721URIStorage)
    {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721Burnable: caller is not owner nor approved");
        super._burn(tokenId);

        // remove the registry and reverse registry mappings
        string memory agent = reverseRegistryMap[tokenId];
        delete reverseRegistryMap[tokenId];
        delete registryMap[agent];
    }

    /**
     * @dev See {IERC721URIStorage-tokenURI}.
     */
    function tokenURI(
        uint256 tokenId
    ) 
        public 
        view 
        virtual 
        override(ERC721URIStorage, ERC721)
        returns (string memory) 
    {
        return ERC721URIStorage.tokenURI(tokenId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721, ERC721Enumerable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }

     /**
     * @dev Returns whether the registry mapping exists already exists.
     */
    function _mappingExists(string memory _name) internal view virtual returns (bool) {
        return !(registryMap[_name] == 0);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override(AccessControlEnumerable, ERC721, ERC721Enumerable)
        returns (bool)
    {
        return super.supportsInterface(interfaceId);
    }
}
